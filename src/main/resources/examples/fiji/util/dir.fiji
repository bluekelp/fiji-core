\ dir.fiji ... list a directory.                      \
\ Uses the class com.SoftWoehr.util.DirSupport        \
\ A good example of using FIJI to test Java classes.  \
\ Part of FIJI http://www.well.com/user/jax/SoftWoehr \
\ GPL'ed Free Software .. NO WARRANTY .. see COPYING. \
\ author: Jack J. Woehr jax@well.com                  \
\ $Id: dir.fiji,v 1.1.1.1 2001/08/21 02:41:29 jwoehr Exp $ \

: getDir \ name -- DirSupport \
    \ See com.SoftWoehr.util.DirSupport for more info. \
    " com.SoftWoehr.util.DirSupport" class new ( 3 roll , )
    ;

: getList \ DirSupport -- String[] \
    dup thicken ( ) drop        \ -- DirSupport \
    getFList ( )                \ -- String[]   \
    ;

: printList \ String[] -- \
    " java.lang.reflect.Array" class getLength \ -- String[] class getLength  \
    ( 3 pick " java.lang.Object" () , )        \ -- String[] Integer          \
    longValue ( )                              \ -- String[] Long             \
    0 do                                       \ -- String[]                  \
      dup                                      \ -- String[] String[]         \
      " java.lang.reflect.Array" class         \ -- String[] String[] class   \
      get ( 3 roll " java.lang.Object" () ,    \ -- String[] class get parm[] \
      0 index Long>intparam , )                \ -- String[] String           \
      . cr                                     \ -- String[]                  \
    loop
    drop
    ;

: dir \ String -- \
    getDir getList printList
    ;

" Type " . cr
"      <dirname> dir"                          . cr
" for a list of a directory called <dirname>." . cr
" e.g." . cr
"      ' . dir"                                . cr
" for a listing of the current directory."     . cr


\ End of dir.fiji \
