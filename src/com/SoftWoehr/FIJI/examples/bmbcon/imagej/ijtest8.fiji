\ ijtest8.fiji ... testing imagej with 8 bit images \
\ Published by B M B con. See accompanying readme.2nd file\
\ Author: Roelf Toxopeus email: postbus@bmbcon2.demon.nl \
\ Last modified: 15 April 2001 rt \

cr " loading image Java test ..." .

\ include ./extensions.fiji \              \ abs \
\ include ./randoms.fiji  \                \ Random and choose \
\ include ./xarrays.fiji   \                \ byte array words \

value h 50 to h		\ height \
value w 50 to w	\ width \
value ip							 \ imageProcessor instance \
value image-plus	\ imagePlus instance \
value pixels				\ pixel array \

\ new greyscale 8bit imageprocessor \
: new-processor	\ w h -- ip \
	ij.process.ByteProcessor class new ( 3 roll Long>intparam , 3 roll Long>intparam , ) ;

\ get the pixel array \
: pixel-array \ ip -- array \
	getPixels ( ) ;

\ don't forget to do this first!!!!!!!!!\
: init-image
	w h new-processor to ip
	ip pixel-array to pixels
	ij.ImagePlus class new ( " ij Test" , ip ij.process.ImageProcessor () , ) to image-plus ;

\ get array length \
: array-length	\ array -- long \
	java.lang.reflect.Array class getLength ( 3 roll java.lang.Object () , )  longValue ( ) ;

\ fills an image with random dots\
: random-ip
	w h * 0 do Random byteValue ( ) 0 index pixels bArray! loop ;

\ a serie of image processing tools ... apply them to see what they do\
: filter-ip \ long -- \
	ip filter ( 3 roll Long>intparam , ) drop ;

: scale-ip	\ long1 long2 -- \
	doubleValue ( ) swap doubleValue ( ) ip scale
	( 3 roll (primitive) , 3 roll (primitive) , ) drop ;

: rotate-ip \ angle(long) -- \
	doubleValue ( ) ip rotate ( 3 roll (primitive) , ) drop ;
 
: outline-ip
	ip outline ( ) drop ;

: erode-ip
	ip erode ( ) drop ;

: noise-ip \ long -- \
	doubleValue ( ) ip noise ( 3 roll (primitive) , ) drop ;

\ use this to update your image\
: update-image
	image-plus updateAndDraw ( ) drop ;

\ puts a image in a window on the screen, do this after init-image\
: show-image
	image-plus show ( ) drop ;

\ serie of shortcuts\
: rup
	random-ip update-image ;

: fup \ long -- \
	filter-ip update-image ;

: oup
	outline-ip update-image ;

: nup	\ long -- \
	noise-ip update-image ;

: eup
	erode-ip update-image ;

\ ============examples=========== \

: pp
	rup begin 50 choose nup 4 choose fup key? until ;

: germs
	rup begin 33 nup 0 fup key? until ;

cr " ... imageJava test loaded" .

\ ***********************eof************************\